! -*-F90-*-
!
module state_equations

  implicit none

contains

  !========================================================================
  function sigma_state(T,S,P)

    !*********************************************************************!
    !     for sigma density by UNESCO(1981)              Gill p.599-601   !
    !---------------------------------------------------------------------!
    !     T : deg C, S : psu, P : bar ---> sigma : kg / m**3              !
    !*********************************************************************!

    real(8)             :: sigma_state
    real(8), intent(in) :: T, S, P

    real(8) :: T2, T3, T4, T5, S2, S32, P2
    real(8) :: RW, R0, CW, C0, C, R

    !------------------------------------------------------------------

    T2  = T  * T
    T3  = T2 * T
    T4  = T2 * T2
    T5  = T3 * T2
    S2  = S  * S
    S32 = S  * SQRT( S )
    P2  = P  * P

    RW = 999.842594E0     + 6.793952E-2 * T  - 9.095290E-3 * T2 &
         &   + 1.001685E-4 * T3 - 1.120083E-6 * T4 + 6.536332E-9 * T5
    R0 = RW &
         &   +   S * ( 0.824493     - 4.0899E-3 * T  + 7.6438E-5 * T2 &
         &                          - 8.2467E-7 * T3 + 5.3875E-9 * T4 ) &
         &   + S32 * ( - 5.72466E-3 + 1.0227E-4 * T  - 1.6546E-6 * T2 ) &
         &   + S2  * 4.8314E-4
    CW = 19652.21 + 1.484206E2  * T  - 2.327105    * T2 &
         &              + 1.360477E-2 * T3 - 5.155288E-5 * T4
    C0 = CW &
         &   + S   * ( 54.6746  - 0.603459   * T &
         &                      + 1.09987E-2 * T2 - 6.1670E-5 * T3 ) &
         &   + S32 * ( 7.944E-2 + 1.6483E-2  * T  - 5.3009E-4 * T2 )
    C  = C0 &
         &   + P  *       ( 3.239908 + 1.43713E-3 * T + 1.16092E-4 * T2 &
         &                                            - 5.77905E-7 * T3 ) &
         &   + P  * S   * ( 2.2838E-3 - 1.0981E-5 * T - 1.6078E-6  * T2 ) &
         &   + P  * S32 *   1.91075E-4 &
         &   + P2 *       ( 8.50935E-5 - 6.12293E-6 * T + 5.2787E-8  * T2 ) &
         &   + P2 * S   * ( -9.9348E-7 + 2.0816E-8  * T + 9.1697E-10 * T2 )

    r  = R0 / ( 1.0 - P/C )
    sigma_state = r - 1000.d0
    
  end function sigma_state
  !=========================================================================
  !=========================================================================
  function alpha_state(T,S)
    !**********************************************************************
    !     Determine thermal expantion on sea surface (P=0)                *
    !        by using equation of state of UNESCO(1981)                   *
    !---------------------------------------------------------------------*
    !     T : deg C, S : psu, P : bar ---> sigma : kg / m**3              *
    !**********************************************************************

    REAL(8) :: ALPHA_state
    REAL(8),intent(in) :: T , S
    REAL(8) :: T2, T3, T4, T5, S2, S32
    REAL(8) :: AW, A0

    !----------------------------------------------------------------------

    T2  = T  * T
    T3  = T2 * T
    T4  = T2 * T2
    T5  = T3 * T2
    S2  = S  * S
    S32 = S  * SQRT( S )

    AW = 6.793952E-2 &
         &     - 2.0d0 * 9.095290E-3 * T &
         &     + 3.0d0 * 1.001685E-4 * T2 &
         &     - 4.0d0 * 1.120083E-6 * T3 &
         &     + 5.0d0 * 6.536332E-9 * T4

    A0 = AW &
         &   + S * ( &
         &     - 4.0899E-3 &
         &     + 2.0d0 * 7.6438E-5 * T &
         &     - 3.0d0 * 8.2467E-7 * T2 &
         &     + 4.0d0 * 5.3875E-9 * T3 ) &
         &   + S32 * ( &
         &     + 1.0227E-4 &
         &     - 2.0d0 * 1.6546E-6 * T )

    ALPHA_state = A0

  end function alpha_state
  !=========================================================================
  !=========================================================================
  function beta_state(T,S)
    !**********************************************************************
    !      Calculate haline contraction on sea surface (P=0)              *
    !       by using equation of state of  UNESCO(1981)                   *
    !---------------------------------------------------------------------*
    !     T : deg C, S : psu, P : bar ---> sigma : kg / m**3              *
    !**********************************************************************

    REAL(8) :: BETA_state
    REAL(8),intent(in) :: T , S
    REAL(8) :: T2, T3, T4, T5, S12
    REAL(8) :: BW, B0

    !----------------------------------------------------------------------

    T2  = T  * T
    T3  = T2 * T
    T4  = T2 * T2
    T5  = T3 * T2
    S12 = SQRT(S)

    BETA_state = &
         &     0.824493d0 - 4.0899d-3 * T + 7.6438d-5 * T2 &
         &     - 8.2467d-7 * T3 + 5.3875d-9 * T4 &
         &     + 1.5d0 * S12 * ( -5.72466d-3 + 1.0227d-4 * T -1.6546d-6 * T2 ) &
         &     + 2.0d0 * S   * 4.8314d-4

  end function beta_state
  !========================================================================
  !=========================================================================
  function alpha_beta(T,S)
    !**********************************************************************
    !      Calculate haline contraction on sea surface (P=0)              *
    !       by using equation of state of  UNESCO(1981)                   *
    !---------------------------------------------------------------------*
    !     T : deg C, S : psu, P : bar ---> sigma : kg / m**3              *
    !**********************************************************************

    REAL(8) :: alpha_beta
    REAL(8),intent(in) :: T , S
    REAL(8) :: T2, T3, T4, T5, S12
    REAL(8) :: BW, B0

    !----------------------------------------------------------------------

    T2  = T  * T
    T3  = T2 * T
    T4  = T2 * T2
    T5  = T3 * T2
    S12 = SQRT(S)

    alpha_beta = &
         &     - 4.0899d-3 + 2.0d0 * 7.6438d-5 * T &
         &     - 3.0d0 * 8.2467d-7 * T2 + 4.0d0 * 5.3875d-9 * T3 &
         &     + 1.5d0 * S12 * ( 1.0227d-4 - 2.0d0 * 1.6546d-6 * T )

  end function alpha_beta
  !========================================================================
  !========================================================================
  function cabbeling_fact(T,S,P)

    ! adpoted from McDougall (1987) J.G.R

    real(8)             :: cabbeling_fact
    real(8), intent(in) :: T, S, P

    real(8) :: T2, T3, T4, T5, S12, Pdb, Pdb2

    !------------------------------------------------------------------

    T2  = T  * T
    T3  = T2 * T
    T4  = T2 * T2
    T5  = T3 * T2
    S12 = SQRT(S)
    Pdb = 0.1d0 * P
    Pdb2 = Pdb * Pdb

    cabbeling_fact = &
         & 0.136108d-4 - 0.325332d-6 * T + 0.663168d-8 * T2 - 0.732603d-10 * T3 &
         & + (S - 35.0d0) * (- 0.106105d-6 + 0.205719d-8 * T) &
         & + Pdb * (-0.891152d-9 + 0.191471d-10 * T) &
         & + Pdb * (S - 35.0d0) * 0.469446d-11 &
         & + Pdb2 * 0.120888d-13 

  end function cabbeling_fact
  !========================================================================
  !========================================================================
  function thermobaric_fact(T,S,P)

    ! adpoted from McDougall (1987) J.G.R

    real(8)             :: thermobaric_fact
    real(8), intent(in) :: T, S, P

    real(8) :: T2, T3, T4, T5, S12, Pdb, Pdb2

    !------------------------------------------------------------------

    T2  = T  * T
    T3  = T2 * T
    T4  = T2 * T2
    T5  = T3 * T2
    S12 = SQRT(S)
    Pdb = 0.1d0 * P
    Pdb2 = Pdb * Pdb

    thermobaric_fact = &
         & 0.299559d-7 - 0.875412d-9 * T + 0.150321d-10 * T2 - 0.139391d-12 * T3 &
         & + (S - 35.0d0) * (- 0.153332d-9 + 0.248555d-11 * T) &
         & + Pdb * (-0.556927d-12 + 0.272974d-13 * T - 0.631646d-15 * T2) &
         & + Pdb2 * (-0.453055d-16)

  end function thermobaric_fact
  !========================================================================

end module state_equations
